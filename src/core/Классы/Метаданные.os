Перем СоответствиеИменID;
Перем Таблица;

Процедура ПриСозданииОбъекта() Экспорт
	
	СоответствиеИменID = Новый Соответствие();
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("ГУИД");
	Таблица.Колонки.Добавить("Порядок");

КонецПроцедуры

Функция СоответствиеИменID() Экспорт
	Возврат СоответствиеИменID;
КонецФункции

Функция ОпределитьИменаМетаданных(ПутьКФайлуДампа) Экспорт
	
	ТекстСодержимогоФайла = ОбщегоНазначения.ПрочитатьФайлВТекст(ПутьКФайлуДампа);

	// заполнение соответствия ID -> Name 
	РВ = Новый РегулярноеВыражение("name=""(?<name>.+?)"" id=""(?<id>.+?)""");
	Совпадения = РВ.НайтиСовпадения(ТекстСодержимогоФайла);
	Для каждого Совпадение Из Совпадения Цикл
		
		ГУИД = Совпадение.Группы.ПоИмени("id").Значение;
		Если СтрНайти(ГУИД, ".") Тогда
			Продолжить;
		КонецЕсли;
		Имя = Совпадение.Группы.ПоИмени("name").Значение;
		СоответствиеИменID.Вставить(ГУИД, Имя);

		СтрТЗ = Таблица.Добавить();
		СтрТЗ.Имя = Имя;
		СтрТЗ.ГУИД = ГУИД;

	КонецЦикла;

	Возврат СоответствиеИменID;

КонецФункции 

Функция СоздатьКэш(ПутьКФайлу, УпорядочитьПринудительно = Ложь) Экспорт

	Если УпорядочитьПринудительно Тогда
		Для каждого Стр Из Таблица Цикл
			Стр.Порядок = ЗначениеУпорядочивания(Стр);
		КонецЦикла;
		Таблица.Сортировать("Порядок");
	КонецЕсли;

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу, КодировкаТекста.UTF8);
	ЗаписьТекста.ЗаписатьСтроку("<?xml version=""1.0"" encoding=""UTF-8""?>");

	Шаблон = "<name=""%1"" id=""%2""/>";
	Для каждого Стр Из Таблица Цикл
		ЗаписьТекста.ЗаписатьСтроку(СтрШаблон(Шаблон, Стр.Имя, Стр.ГУИД));
	КонецЦикла;

	ЗаписьТекста.Закрыть();

КонецФункции 

Процедура Преобразовать(пТаблица) Экспорт
	Таблица.Очистить();
	Для каждого Стр1 Из пТаблица Цикл
		Стр2 = Таблица.Добавить();
		Стр2.Имя = Стр1.Имя;
		Стр2.ГУИД = Стр1.ГУИД;
	КонецЦикла;
КонецПроцедуры

Функция ЗначениеУпорядочивания(СтрТЗ)

	Имя = СтрТЗ.Имя;
	Если СтрНайти(Имя, ".Attribute")
		Или СтрНайти(Имя, ".TabularSection")
		Или СтрНайти(Имя, ".Dimension")
		Или СтрНайти(Имя, ".Resource") Тогда
		ЗначениеУпорядочивания = Лев(Имя, СтрНайти(Имя, ".", , , 2) - 1) + " " + СтрТЗ.Гуид;
	Иначе
		ЗначениеУпорядочивания = Имя;
	КонецЕсли;

	Возврат ЗначениеУпорядочивания;

КонецФункции