Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("REPO", ".", "Путь к git-репозиторию")
				.ТСтрока();

	Команда.Опция("in mod", "src/Ext/ParentConfigurations_mod.bin", 
				"Относительный путь к сконвертированному файлу ParentConfigurations_mod.bin внутри репозитория")
				.ТСтрока();

	Команда.Опция("out orig", "src/Ext/ParentConfigurations.bin", 
				"Относительный путь к восстановленному файлу внутри репозитория")
				.ТСтрока();

	Команда.Опция("r recovery", "src/ConfigID.txt", 
				"Относительный путь к файлу пар Id-Name файлу внутри репозитория. Значения будут восстановлены из файла опции --in={XXX}")
				.ТСтрока();

КонецПроцедуры

// Выполняет логику команды
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Лог = ПараметрыПриложения.Лог();

	КаталогРепозитория          = Команда.ЗначениеАргумента("REPO");
	ПутьККонвертированномуФайлу = Команда.ЗначениеОпции("in");
	ПутьКВосстановленномуФайлу  = Команда.ЗначениеОпции("out");
	ВосстановитьДанныеДампФайла = Команда.ЗначениеОпции("r");
	
	Файл = Новый Файл(КаталогРепозитория);
	КаталогРепозитория = Файл.ПолноеИмя;

	УстановитьТекущийКаталог(КаталогРепозитория);
	
	ИмяВременногоФайлаЦелевой = ПолучитьИмяВременногоФайла();

	Конвертор = Новый Конвертор(КаталогРепозитория);
	Если ЗначениеЗаполнено(ВосстановитьДанныеДампФайла) Тогда
		Конвертор.ВосстанавливатьДампВФайл(ВосстановитьДанныеДампФайла);
	КонецЕсли;

	Успех = Конвертор.ВыполнитьВосстановление(ПутьККонвертированномуФайлу, ИмяВременногоФайлаЦелевой);
	Если НЕ Успех Тогда
		Лог.Предупреждение("Восстановление файла поддержки не выполнено. Нет данных к восстановлению.");
		Возврат;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ПутьКВосстановленномуФайлу) Тогда
		ПутьКВосстановленномуФайлу = ПутьККонвертированномуФайлу;
	КонецЕсли;

	УдалитьФайлы(ПутьКВосстановленномуФайлу);
	ПереместитьФайл(ИмяВременногоФайлаЦелевой, ПутьКВосстановленномуФайлу);

	Лог.Информация("Выполнено восстановление файла поддержки %1", ПутьКВосстановленномуФайлу);

КонецПроцедуры

