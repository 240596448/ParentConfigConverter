///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("SRC", "", "Путь к каталогу исходников src/")
				.ТСтрока()
				.Обязательный(Истина);

	Команда.Опция("file", "Ext/ParentConfigurations.bin", 
				"Относительный путь к файлу ParentConfigurations.bin внутри каталога исходников")
				.ТСтрока();

	Команда.Опция("out", "", 
				"Относительный путь файла-результата. По умолчанию файл будет перезаписан по исходному пути file")
				.ТСтрока();

КонецПроцедуры

// Выполняет логику команды
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	КаталогИсходников           = Команда.ЗначениеАргумента("SRC");
	ПутьККонвертированномуФайлу = Команда.ЗначениеОпции("file");
	ПутьКВосстановленномуФайлу  = Команда.ЗначениеОпции("out");
	
	УстановитьТекущийКаталог(КаталогИсходников);
	
	ИмяВременногоФайлаЦелевой = ПолучитьИмяВременногоФайла();

	Конвертор = Новый Конвертор(КаталогИсходников);
	Конвертор.ВыполнитьВосстановление(ПутьККонвертированномуФайлу, ИмяВременногоФайлаЦелевой);

	Если НЕ ЗначениеЗаполнено(ПутьКВосстановленномуФайлу) Тогда
		ПутьКВосстановленномуФайлу = ПутьККонвертированномуФайлу;
	КонецЕсли;

	УдалитьФайлы(ПутьКВосстановленномуФайлу);
	ПереместитьФайл(ИмяВременногоФайлаЦелевой, ПутьКВосстановленномуФайлу);

КонецПроцедуры

