///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("SRC", "", "Путь к каталогу исходников src/")
				.ТСтрока()
				.Обязательный(Истина);

	Команда.Опция("file", "Ext/ParentConfigurations.bin", 
				"Относительный путь к файлу ParentConfigurations.bin внутри каталога исходников")
				.ТСтрока();

	Команда.Опция("out", "", 
				"Относительный путь файла-результата. По умолчанию файл будет перезаписан по исходному пути file")
				.ТСтрока();

КонецПроцедуры

// Выполняет логику команды
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Лог = ПараметрыПриложения.Лог();

	КаталогИсходников           = Команда.ЗначениеАргумента("SRC");
	ПутьККонвертированномуФайлу = Команда.ЗначениеОпции("file");
	ПутьКВосстановленномуФайлу  = Команда.ЗначениеОпции("out");
	
	Файл = Новый Файл(КаталогИсходников);
	КаталогИсходников = Файл.ПолноеИмя;

	УстановитьТекущийКаталог(КаталогИсходников);
	
	ИмяВременногоФайлаЦелевой = ПолучитьИмяВременногоФайла();

	Конвертор = Новый Конвертор(КаталогИсходников);
	Успех = Конвертор.ВыполнитьВосстановление(ПутьККонвертированномуФайлу, ИмяВременногоФайлаЦелевой);
	Если НЕ Успех Тогда
		Лог.Предупреждение("Восстановление файла поддержки не выполнено. Нет данных к восстановлению.");
		Возврат;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ПутьКВосстановленномуФайлу) Тогда
		ПутьКВосстановленномуФайлу = ПутьККонвертированномуФайлу;
	КонецЕсли;

	УдалитьФайлы(ПутьКВосстановленномуФайлу);
	ПереместитьФайл(ИмяВременногоФайлаЦелевой, ПутьКВосстановленномуФайлу);

	Лог.Информация("Выполнено восстановление файла поддержки %1", ПутьКВосстановленномуФайлу);

КонецПроцедуры

