Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("REPO", ".", "Путь к git-репозиторию")
				.ТСтрока();

	Команда.Опция("in mod", "src/Ext/ParentConfigurations_mod.bin", 
				"Относительный путь к сконвертированному файлу ParentConfigurations_mod.bin внутри репозитория")
				.ТСтрока();

	Команда.Опция("out orig", "src/Ext/ParentConfigurations.bin", 
				"Относительный путь к восстановленному файлу внутри репозитория")
				.ТСтрока();

	Команда.Опция("dump-recovery", "src/ConfigID.xml", 
				"Относительный путь к файлу пар Id-Name файлу внутри репозитория. Значения будут восстановлены из модифицированного файла (опция --in)")
				.ТСтрока();

КонецПроцедуры

// Выполняет логику команды
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Лог = ПараметрыПриложения.Лог();

	КаталогРепозитория          = Команда.ЗначениеАргумента("REPO");
	ПутьККонвертированномуФайлу = Команда.ЗначениеОпции("in");
	ПутьКВосстановленномуФайлу  = Команда.ЗначениеОпции("out");
	ВосстановитьДанныеДампФайла = Команда.ЗначениеОпции("dump-recovery");
	
	Файл = Новый Файл(КаталогРепозитория);
	КаталогРепозитория = Файл.ПолноеИмя;

	УстановитьТекущийКаталог(КаталогРепозитория);

	Файл = Новый Файл(ПутьКВосстановленномуФайлу);
	Если Файл.Существует() И Файл.ЭтоКаталог() Тогда
		Лог.Ошибка("Файл --out=%1 не может быть каталогом", ПутьКВосстановленномуФайлу);
		ВызватьИсключение "";
	КонецЕсли;

	ИмяВременногоФайлаЦелевой = ПолучитьИмяВременногоФайла();

	Конвертор = Новый Конвертор(КаталогРепозитория);
	Успех = Конвертор.ВыполнитьВосстановление(ПутьККонвертированномуФайлу, ИмяВременногоФайлаЦелевой);
	// Успех = Истина;
	Если НЕ Успех Тогда
		Лог.Предупреждение("Восстановление файла поддержки не выполнено. Нет данных к восстановлению.");
		Возврат;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ПутьКВосстановленномуФайлу) Тогда
		ПутьКВосстановленномуФайлу = ПутьККонвертированномуФайлу;
	КонецЕсли;

	УдалитьФайлы(ПутьКВосстановленномуФайлу);
	ПереместитьФайл(ИмяВременногоФайлаЦелевой, ПутьКВосстановленномуФайлу);

	Лог.Информация("Выполнено восстановление файла поддержки %1", ПутьКВосстановленномуФайлу);

	// dump-recovery
	Если ЗначениеЗаполнено(ВосстановитьДанныеДампФайла) Тогда
	 	Таблица = Конвертор.ПолучитьТаблицуОбъектовНаПоддержке(ПутьККонвертированномуФайлу);
		Метаданные = Новый Метаданные;
		Метаданные.Преобразовать(Таблица);
		Метаданные.СоздатьКэш(ВосстановитьДанныеДампФайла, Истина);

		Лог.Информация("Кэш идентификаторов метаданных %1 восстановлен из файла поддержки %2", ВосстановитьДанныеДампФайла, ПутьККонвертированномуФайлу);
	КонецЕсли;

КонецПроцедуры

