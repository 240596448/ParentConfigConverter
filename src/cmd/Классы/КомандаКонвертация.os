#Использовать fs
//#Использовать "..\..\core"

///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("SRC", "", "Путь к каталогу исходников src/")
				.ТСтрока()
				.Обязательный(Истина);

	Команда.Опция("file", "Ext/ParentConfigurations.bin", "Относительный путь к файлу ParentConfigurations.bin внутри каталога исходников")
				.ТСтрока();
				
	Команда.Опция("dump", "ConfigDumpInfo.xml", "Путь к файлу ConfigDumpInfo.xml внутри каталога исходников")
				.ТСтрока();
				
	Команда.Опция("bak backup", Ложь, "Сохранять оригинальный файл в ParentConfigurations.bin.bak")
				.Флаг();
				
КонецПроцедуры

// Выполняет логику команды
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	КаталогИсходников   = Команда.ЗначениеАргумента("SRC");
	ПутьКФайлуПоддержки = Команда.ЗначениеОпции("file");
	ПутьКФайлуДампа     = Команда.ЗначениеОпции("dump");
	СоздаватьБэкап      = Команда.ЗначениеОпции("backup");
	
	УстановитьТекущийКаталог(КаталогИсходников);
	
	ИмяВременногоФайлаИсходный = ПолучитьИмяВременногоФайла();
	КопироватьФайл(ПутьКФайлуПоддержки, ИмяВременногоФайлаИсходный);

	ИмяВременногоФайлаЦелевой = ПолучитьИмяВременногоФайла();

	Конвертор = Новый Конвертор(КаталогИсходников);
	Конвертор.ОпределитьИменаМетаданных(ПутьКФайлуДампа);
	Конвертор.ВыполнитьКонвертацию(ИмяВременногоФайлаИсходный, ИмяВременногоФайлаЦелевой);

	УдалитьФайлы(ПутьКФайлуПоддержки);
	ПереместитьФайл(ИмяВременногоФайлаЦелевой, ПутьКФайлуПоддержки);

	Если СоздаватьБэкап Тогда
		ФайлПоддержки = Новый Файл(ПутьКФайлуПоддержки);
		ИмяФайлаПоддержки = ФайлПоддержки.Имя;
		ИмяФайлаБэкапа = ОбъединитьПути(ФайлПоддержки.Путь, ИмяФайлаПоддержки + ".bak");
		УдалитьФайлы(ИмяФайлаБэкапа);
		ПереместитьФайл(ИмяВременногоФайлаИсходный, ИмяФайлаБэкапа);
	Иначе
		УдалитьФайлы(ИмяВременногоФайлаИсходный);
	КонецЕсли;

КонецПроцедуры

