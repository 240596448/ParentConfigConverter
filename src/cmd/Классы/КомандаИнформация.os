Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("SRC", ".", "Путь к каталогу исходников src или другая папка, относительно которй будут указаны пути опций file и dump")
				.ТСтрока()
				.Обязательный(Истина);

	Команда.Опция("file", "Ext/ParentConfigurations.bin", 
				"Относительный путь к файлу ParentConfigurations.bin внутри каталога исходников")
				.ТСтрока();
				
	Команда.Опция("dump", "ConfigDumpInfo.xml", 
				"Путь к файлу ConfigDumpInfo.xml внутри каталога исходников")
				.ТСтрока();
				
	Команда.Опция("added", False, "Вывести в консоль объекты без поддержки (добавленные в основную конефигурацию), булево")
				.Флаг();

	Команда.Опция("filter", "", "Вывести в консоль объекты с определенным статусом поддержки в формате '\d,\d' (например, '1,0')")
				.ТСтрока();
	
КонецПроцедуры

// Выполняет логику команды
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Лог = ПараметрыПриложения.Лог();

	КаталогИсходников   = Команда.ЗначениеАргумента("SRC");
	ПутьКФайлуПоддержки = Команда.ЗначениеОпции("file");
	ПутьКФайлуДампа     = Команда.ЗначениеОпции("dump");

	ОпцияБезПоддержки        = Команда.ЗначениеОпции("added");
	ОпцияФильроватьПоСтатусу = Команда.ЗначениеОпции("filter");

	Если НЕ ОпцияБезПоддержки
		И НЕ ЗначениеЗаполнено(ОпцияФильроватьПоСтатусу) Тогда
		Лог.Предупреждение("Не выбрана ни одна опция вывода информации. Укажите опции 'added' и(или) 'filter'");
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(КаталогИсходников);
	КаталогИсходников = Файл.ПолноеИмя;
	
	УстановитьТекущийКаталог(КаталогИсходников);
	
	Метаданные = Новый Метаданные();
	Метаданные.ОпределитьИменаМетаданных(ПутьКФайлуДампа);
	СоответствиеИменID = Метаданные.СоответствиеИменID();

	Конвертор = Новый Конвертор(КаталогИсходников);
	Таблица = Конвертор.ПолучитьТаблицуОбъектовНаПоддержке(ПутьКФайлуПоддержки);
	
	СоотвествиеIDПоддержки = Новый Соответствие();
	Для каждого Стр Из Таблица Цикл
		СоотвествиеIDПоддержки.Вставить(Стр.ГУИД, Стр.Режим);
	КонецЦикла;
	
	Если ОпцияБезПоддержки Тогда
		Сообщить(" --- Объекты без поддержки (добавленные в конфигурацию) --- ");
		Для каждого КЗ Из СоответствиеИменID Цикл
			Если СоотвествиеIDПоддержки[КЗ.Ключ] = Неопределено Тогда
				Сообщить(КЗ.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОпцияФильроватьПоСтатусу) Тогда
		Сообщить(" --- Объекты со статусом поддержки '" + ОпцияФильроватьПоСтатусу + "' --- ");
		Для каждого Стр Из Таблица Цикл
			Если Стр.Режим = ОпцияФильроватьПоСтатусу Тогда
				Если ЗначениеЗаполнено(Стр.Имя) Тогда
					Сообщить(Стр.Имя);
				ИначеЕсли СоответствиеИменID[Стр.ГУИД] <> Неопределено Тогда
					// для неконвертированного файла
					Сообщить(СоответствиеИменID[Стр.ГУИД]);
				КонецЕсли;

				// данный способ сопоставления намеренно упрощен по сравнению с конвертацией
				// гуиды в файле поддержки могут повторяться, если поставок несколько
				// тогда в основной поставке будет парный гуид, а в "дополнительных" - непарный, часто с режимом 2,0

			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

